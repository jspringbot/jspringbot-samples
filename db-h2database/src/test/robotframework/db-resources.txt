*** Settings ***
Library           JSpringBotGlobal

*** Variables ***
${DELETE_ACCOUNTS_QUERY}            delete.accounts
${ADD_ACCOUNT_QUERY}                insert.account
${VERIFY_ACCOUNT_QUERY}             verify.account.by.name
${COUNT_ACCOUNTS_QUERY}             get.account.size
${INSERT_AFFECTED_ROWS_EXPECTED}    1


*** Keywords ***
DB Setup
    DB Begin
    Delete All Accounts

DB TearDown
    DB Rollback

Account Size Should Be
    [Arguments]     ${size}
    Create DB Query By Name     ${COUNT_ACCOUNTS_QUERY}
    Execute DB Query
    DB Projected Count Should Be   ${size}

Delete All Accounts
   Create DB Query By Name     ${DELETE_ACCOUNTS_QUERY}
   Execute DB Update

Add Account
    [Arguments]     ${accountName}     ${description}      ${userName}
    Create DB Query By Name    ${ADD_ACCOUNT_QUERY}
    Set DB String Parameter    name           ${accountName}
    Set DB String Parameter    description    ${description}
    Set DB String Parameter    userName       ${userName}
    ${affectedRows}=    Execute DB Update
    Should Be Equal As Integers     ${affectedRows}     ${INSERT_AFFECTED_ROWS_EXPECTED}   There should ${INSERT_AFFECTED_ROWS_EXPECTED} affected rows.

Verify Account By Name Should Exist
    [Arguments]     ${accountName}
    Create DB Query By Name    ${VERIFY_ACCOUNT_QUERY}
    Set DB String Parameter    name           ${accountName}
    Execute DB Query
    DB Projected Count Is Not Zero
